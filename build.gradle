buildscript {
	ext.kotlinVersion = '1.1.3-2'
	ext.springBootVersion = '1.5.6.RELEASE'
	repositories {
		mavenCentral()
		maven { url 'http://repo.spring.io/milestone' }
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
		classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
	}
}

allprojects {
	apply plugin: 'lesscss'
	apply plugin: 'java'
	apply plugin: 'idea'
}
apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'spring-boot'

apply from: 'libs.gradle'

subprojects {
	apply plugin: 'java'
	apply plugin: 'kotlin'
}

def games = project(':Games').getChildProjects().values()

// Source sets for other projects are not available until after they are evaluated.
gradle.projectsEvaluated {
	processResources {
		from games*.sourceSets*.main.resources.srcDirs
		dependsOn 'lesscss'
	} << {
		println '*** Processed resources'
	}
}
allprojects {
	repositories {
		mavenCentral()
		maven { url 'http://repo.spring.io/milestone' }
	}

	lesscss {
		source = fileTree('src/main/resources/static/less') {
			include '**/*.less'
		}
		dest = 'src/main/resources/static/css'
	}

	build {
		dependsOn 'lesscss'
	}

	version = '1.0'
	sourceCompatibility = 1.8

	compileJava.options.encoding = 'UTF-8'

	dependencies {
		compile 'org.slf4j:jcl-over-slf4j:1.7.25'
		compile 'org.jetbrains:annotations:15.0'
		compile files(System.properties['user.home'] + '/.lib/java/Ophelia/Ophelia.jar')
		compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

		testCompile group: 'junit', name: 'junit', version: '4.12'
		testCompile 'org.hamcrest:hamcrest-all:1.3'
	}
	compileKotlin {
		kotlinOptions {
			jvmTarget = "1.8"
		}
	}
	compileTestKotlin {
		kotlinOptions {
			jvmTarget = "1.8"
		}
	}
}

dependencies {
	compile project(':Core'),
			games
	compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
	compile "org.springframework:spring-context:$springVersion"
	compile "org.springframework:spring-web:$springVersion"
	compile "org.springframework:spring-webmvc:$springVersion"
	compile "org.springframework:spring-websocket:$springVersion"
	compile "org.springframework:spring-messaging:$springVersion"
	compile 'io.projectreactor:reactor-net:2.0.8.RELEASE'
	compile 'io.netty:netty-all:4.0.40.Final'
	compile "org.springframework.security:spring-security-web:$springSecurityVersion"
	compile "org.springframework.security:spring-security-config:$springSecurityVersion"
	compile "org.springframework.security:spring-security-messaging:$springSecurityVersion"
	compile "org.springframework.session:spring-session:$springSessionVersion"
	compile 'org.codehaus.groovy:groovy-templates:2.4.7'
	compile 'ch.qos.logback:logback-classic:1.1.11'
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'com.codepoetics:protonpack:1.9'

	testCompile group: 'org.springframework', name: 'spring-test', version: "$springVersion"
	testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}