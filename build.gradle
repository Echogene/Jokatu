buildscript {
	repositories {
		mavenCentral()
		maven { url 'http://repo.spring.io/milestone' }
	}

	dependencies {
		classpath('org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE')
		classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
	}
}

allprojects {
	apply plugin: 'lesscss'
	apply plugin: 'java'
	apply plugin: 'idea'
}
apply plugin: 'groovy'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
version = '1.0'

repositories {
	mavenCentral()
	maven { url 'http://repo.spring.io/milestone' }
}

def games = project(':Games').getChildProjects().values()

// Source sets for other projects are not available until after they are evaluated.
gradle.projectsEvaluated {
	processResources {
		from games*.sourceSets*.main.resources.srcDirs
	}
}
allprojects {
	lesscss {
		source = fileTree('src/main/resources/static/less') {
			include '**/*.less'
		}
		dest = 'src/main/resources/static/css'
	}

	build {
		dependsOn 'lesscss'
	}
}

compileJava.options.encoding = 'UTF-8'

dependencies {
	compile project(':Core'),
			games
	compile 'org.springframework.boot:spring-boot-starter-web:1.3.5.RELEASE'
	compile 'org.springframework:spring-context:4.2.6.RELEASE'
	compile 'org.springframework:spring-web:4.2.6.RELEASE'
	compile 'org.springframework:spring-webmvc:4.2.6.RELEASE'
	compile 'org.springframework:spring-websocket:4.2.6.RELEASE'
	compile 'org.springframework:spring-messaging:4.2.6.RELEASE'
	compile 'io.projectreactor:reactor-net:2.0.7.RELEASE'
	compile 'io.netty:netty-all:4.0.34.Final'
	compile 'org.springframework.security:spring-security-web:4.1.0.RELEASE'
	compile 'org.springframework.security:spring-security-config:4.1.0.RELEASE'
	compile 'org.springframework.security:spring-security-messaging:4.1.0.RELEASE'
	compile 'org.springframework.session:spring-session:1.0.2.RELEASE'
	compile 'org.codehaus.groovy:groovy-templates:2.4.3'
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'org.jetbrains:annotations:15.0'
	compile files(System.properties['user.home'] + '/.lib/java/Ophelia/Ophelia.jar')
	testCompile group: 'junit', name: 'junit', version: '4.12'
}