buildscript {
	repositories {
		mavenCentral()
		maven { url 'http://repo.spring.io/milestone' }
	}

	dependencies {
		classpath('org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE')
		classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
	}
}

allprojects {
	apply plugin: 'lesscss'
	apply plugin: 'java'
	apply plugin: 'idea'
}
apply plugin: 'groovy'
apply plugin: 'spring-boot'

apply from: 'libs.gradle'

subprojects {
	apply plugin: 'java'
}

def games = project(':Games').getChildProjects().values()

// Source sets for other projects are not available until after they are evaluated.
gradle.projectsEvaluated {
	processResources {
		from games*.sourceSets*.main.resources.srcDirs
		dependsOn 'lesscss'
	}
}
allprojects {
	repositories {
		mavenCentral()
		maven { url 'http://repo.spring.io/milestone' }
	}

	lesscss {
		source = fileTree('src/main/resources/static/less') {
			include '**/*.less'
		}
		dest = 'src/main/resources/static/css'
	}

	build {
		dependsOn 'lesscss'
	}

	version = '1.0'
	sourceCompatibility = 1.8

	compileJava.options.encoding = 'UTF-8'

	dependencies {
		compile 'org.slf4j:jcl-over-slf4j:1.7.21'
		compile 'org.jetbrains:annotations:15.0'
		compile files(System.properties['user.home'] + '/.lib/java/Ophelia/Ophelia.jar')

		testCompile group: 'junit', name: 'junit', version: '4.12'
		testCompile 'org.hamcrest:hamcrest-all:1.3'
	}
}

dependencies {
	compile project(':Core'),
			games
	compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
	compile "org.springframework:spring-context:$springVersion"
	compile "org.springframework:spring-web:$springVersion"
	compile "org.springframework:spring-webmvc:$springVersion"
	compile "org.springframework:spring-websocket:$springVersion"
	compile "org.springframework:spring-messaging:$springVersion"
	compile 'io.projectreactor:reactor-net:2.0.8.RELEASE'
	compile 'io.netty:netty-all:4.0.40.Final'
	compile "org.springframework.security:spring-security-web:$springSecurityVersion"
	compile "org.springframework.security:spring-security-config:$springSecurityVersion"
	compile "org.springframework.security:spring-security-messaging:$springSecurityVersion"
	compile "org.springframework.session:spring-session:$springSessionVersion"
	compile 'org.codehaus.groovy:groovy-templates:2.4.7'
	compile 'ch.qos.logback:logback-classic:1.1.7'
	compile 'org.apache.commons:commons-lang3:3.4'

	testCompile group: 'org.springframework', name: 'spring-test', version: "$springVersion"
	testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}